plugins {
	id 'org.springframework.boot' version '2.2.1.RELEASE' apply true
  id "io.spring.dependency-management" version "1.0.8.RELEASE"
  id 'java'
	id 'idea'
  id 'io.franzbecker.gradle-lombok' version '3.0.0'
	id 'com.gorylenko.gradle-git-properties' version '2.1.0'

	id 'com.palantir.docker' version '0.22.1'
  id 'com.palantir.docker-run' version '0.22.1'
  id 'com.avast.gradle.docker-compose' version '0.9.4'
	id 'com.google.cloud.tools.jib' version '1.5.1'

  id 'org.unbroken-dome.helm' version '0.4.0'
  id 'org.unbroken-dome.helm-publish' version '0.4.0'
  id 'org.unbroken-dome.helm-releases' version '0.4.0'

  id 'se.patrikerdes.use-latest-versions' version '0.2.12'
  id 'com.github.ben-manes.versions' version '0.24.0'

  id 'maven-publish'
}

configurations {
  developmentOnly
  runtimeClasspath {
    extendsFrom developmentOnly
  }
}

ext {
	set('activeSpringProfiles', 'local')
  set('tag', 'local')
  set('chartRepoUrl', 'http://chartmuseum.default.svc.cluster.local/')
  springcloudversion = 'Hoxton.RELEASE'
  junitplatform = '1.5.2'
  junitjupiter = '5.5.2'
  powermockVersion = '2.0.2'
  spockversion = '1.3-groovy-2.5'
  spockReportsVersion = '1.6.2'
  mockitoversion = '2.28.2'
  groovyversion = '2.5.8'
  allureVersion = '2.12.1'
  togglzversion = '2.6.1.Final'
}

group = 'com.gregturnquist.learningspringboot'
// version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
  maven { url 'https://repository.jboss.org/nexus/content/repositories/public/' }
	maven { url 'https://repo.spring.io/milestone' }
}
configurations {
  compile.exclude module: "spring-boot-starter-tomcat"
  compile.exclude module: "spring-boot-starter-jetty"
}

dependencies {
  implementation enforcedPlatform("org.springframework.cloud:spring-cloud-dependencies:${springcloudversion}")
//  implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
  implementation ('org.springframework.boot:spring-boot-starter-websocket'){
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-web'
  }
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'
	implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
	implementation 'org.springframework.cloud:spring-cloud-starter-stream-rabbit'
	implementation 'org.springframework.cloud:spring-cloud-stream-reactive:2.2.1.RELEASE'
	implementation 'org.springframework.session:spring-session-data-mongodb'
	implementation 'io.micrometer:micrometer-core'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
  // JUnit 5
  testImplementation "org.junit.platform:junit-platform-launcher:${junitplatform}"
  testImplementation "org.junit.platform:junit-platform-engine:${junitplatform}"
  testImplementation "org.junit.platform:junit-platform-commons:${junitplatform}"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitjupiter}"
  testImplementation "org.junit.vintage:junit-vintage-engine:${junitjupiter}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitjupiter}"
  testImplementation "org.mockito:mockito-core:${mockitoversion}"
  testImplementation "org.mockito:mockito-junit-jupiter:${mockitoversion}"
  testImplementation "org.powermock:powermock-api-mockito2:${powermockVersion}"
  // feature togglz
  testImplementation "org.togglz:togglz-junit5:${togglzversion}"
  testImplementation "org.togglz:togglz-junit:${togglzversion}"
  // spock test framework
  // Groovy
  testImplementation "org.codehaus.groovy:groovy-all:${groovyversion}"
  testImplementation "org.spockframework:spock-core:${spockversion}"
  testImplementation "org.spockframework:spock-spring:${spockversion}"
  testImplementation("com.athaydes:spock-reports:${spockReportsVersion}") {
    transitive = false // this avoids affecting your version of Groovy/Spock
  }
  testImplementation 'com.blogspot.toomuchcoding:spock-subjects-collaborators-extension:1.2.2'
  testRuntime 'cglib:cglib-nodep:3.2.12'
  testRuntime 'org.objenesis:objenesis:3.0.1'
  // allure test reporting
  //testRuntimeOnly "com.squareup.retrofit2:converter-gson:${allureVersion}"
  testRuntimeOnly "io.qameta.allure:allure-okhttp3:${allureVersion}"
//  testRuntimeOnly "io.qameta.allure:allure-spock:${allureVersion}"
  testRuntimeOnly "io.qameta.allure:allure-junit4:${allureVersion}"
  testRuntimeOnly "io.qameta.allure:allure-junit5:${allureVersion}"
}

sourceCompatibility = 11
targetCompatibility = 11

lombok {
	version = '1.18.4'
	sha256 = ""
}

springBoot {
  mainClassName = 'com.greglturnquist.learningspringboot.chat.LearningSpringBootChatApplication'
  buildInfo {
    properties {
      artifact = "${rootProject.name}"
      group = "${project.group}"
      name = "${rootProject.name}"
      version = "${project.version}"
    }
  }
}
bootJar {
  mainClassName = 'com.greglturnquist.learningspringboot.chat.LearningSpringBootChatApplication'
  baseName = 'lsb2-chat'
  excludeDevtools = true
  archiveName = 'app.jar'
  manifest {
    attributes 'Implementation-Title': 'OBC Brandshopupdater Backend',
        'Implementation-Version': version,
        'Built-By': System.getProperty('user.name'),
        'Built-Date': new Date(),
        'Built-JDK': System.getProperty('java.version'),
        'Built-Gradle': gradle.gradleVersion
  }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.gregturnquist.learningspringboot'
            from components.java
        }
    }
}

helm {
  // basic helm plugin config
  home = file("${project.buildDir}/helm/home")
  outputDir = file("${project.buildDir}/helm")
  // The global lint configuration applies to all charts
  lint {
      // treat linter warnings as errors (failing the build)
      strict = false
      enabled = true
  }
  filtering {
    // This value will be resolvable in all charts' YAML files as ${authorName}
    values.put 'authorName', 'Developer'
    values.put 'image.tag', project.version.toString()
  }
  charts {
    'lsb2-chat' {
        chartName = project.name
        chartVersion = project.version.toString()
        sourceDir = file('src/main/helm/lsb2-chat')
    }
  }
  repositories {
     museum {
         url = uri(chartRepoUrl)
     }
  }
  // helm-release plugin config
  releases {
      'lsb2-chat' {
          from charts.'lsb2-chat'
          namespace = 'local'
          replace = false
          purge = true
          dryRun = false
          wait = false
          // pass values (like --set on the command line)
          values = [ 'image.tag': project.version ]
          // pass value files (like -f on the command line)
          valueFiles.from "src/main/helm/lsb2-chat/values.yaml"
          valueFiles.from "src/main/helm/lsb2-chat/values-${activeSpringProfiles}.yaml"
      }
  }

  // helm-publish plugin config
  publishing {
      repositories {
         museum {
             url = uri(chartRepoUrl)
         }
      }
  }
}
/*
 * docker-compose plugin configuration.
 * Details on https://github.com/avast/gradle-docker-compose-plugin
 */
dockerCompose {
  useComposeFiles = ['docker/env/local.yml'] // like 'docker-compose -f <file>'; default is empty
  buildBeforeUp = true // performs 'docker-compose build' before calling the 'up' command; default is true

  waitForTcpPorts = true // turns off the waiting for exposed TCP ports opening
  captureContainersOutput = true // if true, prints output of all containers to Gradle output - very useful for debugging; default is false

  stopContainers = true // doesn't call `docker-compose down` - see below the paragraph about reconnecting
  removeContainers = true
  removeOrphans = false // removes containers for services not defined in the Compose file

  environment.put 'TAG', version
}
composeBuild.dependsOn bootJar
composeUp.dependsOn composeBuild
composePush.dependsOn composeBuild

jib {
  from {
    image = 'azul/zulu-openjdk-alpine:11.0.3'
  }
  to {
    image = 'jghamburg/lsb2-chat:local'
//    credHelper = 'osxkeychain'
  }
  container {
    jvmFlags = ['-Djava.security.egd=file:/dev/./urandom', '-Xms512m', '-Xdebug', "-Dspring.profiles.active=local"]
    mainClass = 'com.greglturnquist.learningspringboot.chat.LearningSpringBootChatApplication'
    args = []
    ports = ['8080']
    format = 'OCI'
  }
}

dockerRun{
  name 'semantic-release'
  image 'jghamburg/git-semantic-release'
  volumes "${projectDir}": '/app'
  ports '7080:5000'
  daemonize false
  clean true
  command 'semantic-release', '-b', 'master', '--no-ci'
  env 'GH_TOKEN': "${System.env.GH_TOKEN}", 'GIT_CREDENTIALS': "${System.env.GIT_CREDENTIALS}"
}
